select	; PowerPlan component details
	domain = build(curdomain ,' (', format(sysdate,"yyyymmdd hhmm;3;q"), ")" )
	, plan_or_phase_description = p_cat.description
	, plan_or_phase_active_ind = p_cat.active_ind
	, plan_or_phase_version = p_cat.version
	, plan_or_phase_currency = if (p_cat.version = 0 or  p_cat.version = p_cat_curr_v.version) "current"
	else "obsolete"
	endif
	, plan_or_phase_status = if (p_cat.beg_effective_dt_tm > sysdate and p_cat.end_effective_dt_tm > sysdate) "testing"
	elseif (p_cat.beg_effective_dt_tm < sysdate and p_cat.end_effective_dt_tm > sysdate) "production"
	elseif (p_cat.end_effective_dt_tm < sysdate) "inactive"
	endif
	, plan_or_phase_type_meaning = p_cat.type_mean
	, plan_display_or_phase_parent = if(p_cat.type_mean in ("CAREPLAN", "PATHWAY")) p_cat.display_description
	elseif(p_cat.type_mean = "PHASE") p_cat_phase_parent.description
	else "-"
	endif
	, plan_type = if(p_cat.type_mean in ("CAREPLAN", "PATHWAY")) uar_get_code_display(p_cat.pathway_type_cd)
	else "-"
	endif
	, comp_clinical_category = uar_get_code_display(p_comp.dcp_clin_cat_cd)
	, comp_clinical_subcategory = uar_get_code_display(p_comp.dcp_clin_sub_cat_cd)
	, comp_sequence = cnvtstring(p_comp.sequence)
	, comp_type = if (p_comp.pathway_catalog_id > 0) uar_get_code_display(p_comp.comp_type_cd)
	elseif (p_rel_source.pw_cat_t_id > 0) "Phase"
	endif
	, comp_name = if (p_comp.comp_type_cd = 10734) "notes excluded from extract, due to rtf formatting tags" ;substring(1,500,pp_note.long_text)	; "Note"
	elseif (p_comp.comp_type_cd in (10736, 7326766, 7057725)) ocs.mnemonic	; "Order", "Prescription" (*Note: different "Prescription" codes for G1 and G2)
	elseif (p_comp.comp_type_cd = 10738) occ.description	; "Result Outcome"
	elseif (p_comp.comp_type_cd = 3606218) p_cat_sub.description	; "Sub Phase"
	elseif (p_rel_source.pw_cat_t_id > 0) p_cat_phase.description	; "Phase"
	elseif (p_comp.pathway_comp_id = 0) ""	; Not a component
	else "-  - component type not configured in extract script - -"
	endif
	, comp_active_in_system = if (p_comp.comp_type_cd = 10734 and pp_note.long_text_id = 0 ) "1"	; blank "Note"
	elseif (p_comp.comp_type_cd = 10734) evaluate (pp_note.active_ind, 0, "0", 1, "1")	; "Note"
	elseif (p_comp.comp_type_cd in (10736, 7326766, 7057725)) evaluate (ocs.active_ind, 0, "0", 1, "1")	; "Order", "Prescription" (*Note: different "Prescription" codes for G1 /G5/G6 and G2/G3/G4)
	elseif (p_comp.comp_type_cd = 10738) evaluate (occ.active_ind, 0, "0",1 , "1")	; "Result Outcome"
	elseif (p_comp.comp_type_cd = 3606218) evaluate (p_cat_sub.active_ind, 0, "0", 1, "1")	; "Sub Phase"
	elseif (p_rel_source.pw_cat_t_id > 0) evaluate (p_cat_phase.active_ind, 0, "0", 1, "1")	; "Phase"
	elseif (p_comp.pathway_comp_id = 0) ""	; Not a component
	else "-  - component type not configured in extract script - -"
	endif
	, comp_active_in_plan = if (p_comp.pathway_catalog_id > 0) evaluate (p_comp.active_ind, 0, "0", 1, "1")
	elseif (p_rel_source.pw_cat_t_id > 0) "1"
	else ""
	endif
	, comp_include = if (p_comp.pathway_comp_id > 0)
	if (p_comp.include_ind = 1) "1"
	else "0"
	endif
	 else ""	; do not return duplicate information if row is non-order sentence, or not first order sentence row
	 endif
	, comp_required = if (p_comp.pathway_comp_id > 0)
	if (p_comp.required_ind = 1) "1"
	else "0"
	endif
	 else ""
	 endif
	, All_fac_vv = if(fac_All.synonym_id > 0) "x" else "" endif
	, Demo1_vv = if(fac_Demo1.synonym_id > 0) "x" else "" endif
	, Foots_vv = if(fac_Foots.synonym_id > 0) "x" else "" endif
	, Sunb_vv = if(fac_Sunb.synonym_id > 0) "x" else "" endif
	, Suns_vv = if(fac_Suns.synonym_id > 0) "x" else "" endif
	, Will_vv = if(fac_Will.synonym_id > 0) "x" else "" endif
	, comp_order_sentence = os.order_sentence_display_line
	, comp_order_sentence_comment = substring(1,500,os_comm.long_text)
	, comp_last_update = format(p_comp.updt_dt_tm, "dd/mm/yyyy hh:mm:ss")
	, comp_last_updater = if(p_comp.pathway_comp_id > 0 and p_comp.updt_id = 0) "0"
	else p_p_comp.name_full_formatted
	endif
	, plan_or_phase_version_catalog_id = p_cat.pathway_catalog_id
	, plan_all_versions_id = if(p_cat.type_mean in ("CAREPLAN", "PATHWAY")) cnvtstring(p_cat.version_pw_cat_id)
	else "-"
	endif
	, comp_id = if (p_comp.pathway_comp_id > 0 )  cnvtstring(p_comp.pathway_comp_id)	; return if row is a component, but either non-order sentence, or first order sentence row
	elseif (p_rel_source.pw_cat_t_id > 0)  cnvtstring(p_rel_source.pw_cat_t_id)	; for components which are other powerplans ("Phase")
	else ""	; do not return duplicate information if row is non-order sentence, or not first order sentence row
	endif
	, comp_os_id = if (pw_os.order_sentence_id > 0) cnvtstring(pw_os.order_sentence_id)
	else ""
	endif
	, domain_alignment_comparison_test = if(p_cat.end_effective_dt_tm > sysdate and p_cat.active_ind = 1)
	build(
	 p_cat.description
	, "|", if (p_cat.beg_effective_dt_tm > cnvtdatetime(curdate, curtime3) and p_cat.end_effective_dt_tm > cnvtdatetime(curdate, curtime3)) "testing"
	elseif (p_cat.beg_effective_dt_tm < cnvtdatetime(curdate, curtime3) and p_cat.end_effective_dt_tm > cnvtdatetime(curdate, curtime3)) "production"
	endif
	, "|", p_cat.type_mean
	, "|", if(p_cat.type_mean in ("CAREPLAN", "PATHWAY")) p_cat.display_description
	 else "-"
	 endif
	, "|", p_comp.sequence
	, "|", if (p_comp.comp_type_cd = 10734) "notes excluded from extract, due to rtf formatting tags" ;substring(1,500,pp_note.long_text)	; "Note"
	 elseif (p_comp.comp_type_cd in (10736, 7326766, 7057725)) ocs.mnemonic	; "Order", "Prescription" (*Note: different "Prescription" codes for G1 and G2)
	 elseif (p_comp.comp_type_cd = 10738) occ.description	; "Result Outcome"
	 elseif (p_comp.comp_type_cd = 3606218) p_cat_sub.description	; "Sub Phase"
	 elseif (p_rel_source.pw_cat_t_id > 0) p_cat_phase.description	; "Phase"
	 elseif (p_comp.pathway_comp_id = 0) ""	; Not a component
	 else "-  - component type not configured in extract script - -"
	 endif
	, "|", os.order_sentence_display_line
	)
	else ""
	endif
	, powerplan_rank = dense_rank() over (partition by 0
	order by p_cat.description_key
	, p_cat.version
	, p_cat.pathway_catalog_id
	)
	,wh_path_alias = cvo_p.alias

from
	pathway_catalog p_cat
	, (left join pathway_catalog p_cat_curr_v on p_cat_curr_v.version_pw_cat_id = p_cat.version_pw_cat_id)
	, (left join pathway_comp p_comp on p_comp.pathway_catalog_id = p_cat.pathway_catalog_id)
	, (left join prsnl p_p_comp	on p_p_comp.person_id = p_comp.updt_id)
;	, (left join code_value clin_cat_cv on clin_cat_cv.code_value = p_comp.dcp_clin_cat_cd)	; for p_comp.dcp_clin_cat_cd sequence
	, (left join long_text pp_note on pp_note.long_text_id = p_comp.parent_entity_id
	    and pp_note.parent_entity_name = "PATHWAY_COMP"	; note within a plan
	)
	, (left join order_catalog_synonym ocs on ocs.synonym_id = p_comp.parent_entity_id)	; synonym (orderable or presription) within a plan
	,(left join code_value_outbound cvo_p on cvo_p.code_value = ocs.catalog_cd
	and cvo_p.contributor_source_cd = 9577728 ; WH_PATH
	)


	, (left join ocs_facility_r fac_All on fac_All.synonym_id = ocs.synonym_id	; used when each virtual view appears in its own column, and only one row per synonym
	 and fac_All.facility_cd = 0	; 'All Facilities'
	)
	, (left join ocs_facility_r fac_Demo1 on fac_Demo1.synonym_id = ocs.synonym_id	; used when each virtual view appears in its own column, and only one row per synonym
	 and fac_Demo1.facility_cd = 4038465	; 'DEMO 1 HOSPITAL' facility from code set 220
	)
	, (left join ocs_facility_r fac_Foots on fac_Foots.synonym_id = ocs.synonym_id	; used when each virtual view appears in its own column, and only one row per synonym
	 and fac_Foots.facility_cd = 85758822	; 'Footscray' facility from code set 220
	)
	, (left join ocs_facility_r fac_Sunb on fac_Sunb.synonym_id = ocs.synonym_id	; used when each virtual view appears in its own column, and only one row per synonym
	 and fac_Sunb.facility_cd = 86163538	; 'Sunbury Day' facility from code set 220
	)
	, (left join ocs_facility_r fac_Suns on fac_Suns.synonym_id = ocs.synonym_id	; used when each virtual view appears in its own column, and only one row per synonym
	 and fac_Suns.facility_cd = 86163400	; 'Sunshine' facility from code set 220
	)
	, (left join ocs_facility_r fac_Will on fac_Will.synonym_id = ocs.synonym_id	; used when each virtual view appears in its own column, and only one row per synonym
	 and fac_Will.facility_cd = 86163477	; 'Williamstown' facility from code set 220
	)
	, (left join outcome_catalog occ on occ.outcome_catalog_id = p_comp.parent_entity_id)
	, (left join pathway_catalog p_cat_sub on p_cat_sub.pathway_catalog_id = p_comp.parent_entity_id)	; sub-phase within a care plan
	, (left join pw_cat_reltn p_rel_source on p_rel_source.pw_cat_s_id = p_cat.pathway_catalog_id	; pathway children (ie phases)
	and p_rel_source.type_mean = "GROUP"	; phase / pathway relationship
	)
	, (left join pathway_catalog p_cat_phase on p_cat_phase.pathway_catalog_id = p_rel_source.pw_cat_t_id)	; phase within pathway
	, (left join pw_comp_os_reltn pw_os on pw_os.pathway_comp_id = p_comp.pathway_comp_id)
	, (left join order_sentence os on os.order_sentence_id = pw_os.order_sentence_id)
	, (left join long_text os_comm on os_comm.long_text_id = os.ord_comment_long_text_id)
	, (left join pw_cat_reltn p_rel_target on p_rel_target.pw_cat_t_id = p_cat.pathway_catalog_id	; phase parent (ie pathway)
	and p_rel_target.type_mean = "GROUP"	; phase / pathway relationship
	)
	, (left join pathway_catalog p_cat_phase_parent on p_cat_phase_parent.pathway_catalog_id = p_rel_target.pw_cat_s_id)

plan	p_cat
where	p_cat.pathway_catalog_id > 0	; to remove dummy row from results
;and	p_cat.pathway_catalog_id in (select pathway_catalog_id	; PowerPlan
;	from pathway_comp
;	where parent_entity_id in (select synonym_id	;containing a synonym pathway component
;	from order_catalog_synonym
;	where active_ind = 0	; that is inactive
;	)
;	)
;and	p_cat.description_key < "M*"
and	p_cat.description_key >= "P*"


join	p_cat_curr_v
where	(
	p_cat_curr_v.version = (select max(version)
	from pathway_catalog
	where version_pw_cat_id = p_cat_curr_v.version_pw_cat_id
	and p_cat.type_mean not in ("DOT", "PHASE")
	)
	or
	p_cat_curr_v.pathway_catalog_id = (select max(pathway_catalog_id)
	from pathway_catalog
	where version_pw_cat_id = p_cat_curr_v.version_pw_cat_id
	and p_cat.type_mean in ("DOT", "PHASE")
	)
	)

join	p_comp
join	p_p_comp
;join	clin_cat_cv
join	pp_note
join	ocs
join	fac_All	; used when each virtual view appears in its own column, and only one row per synonym
join	fac_Demo1	; used when each virtual view appears in its own column, and only one row per synonym
join	fac_Foots	; used when each virtual view appears in its own column, and only one row per synonym
join	fac_Sunb	; used when each virtual view appears in its own column, and only one row per synonym
join	fac_Suns	; used when each virtual view appears in its own column, and only one row per synonym
join	fac_Will	; used when each virtual view appears in its own column, and only one row per synonym
join	occ
join	p_cat_sub
join	p_rel_source
join	p_cat_phase
join	pw_os
join	os
join	os_comm
join	p_rel_target
join	p_cat_phase_parent
join 	cvo_p

order by
	p_cat.description_key
	, p_cat.version
	, p_cat.pathway_catalog_id
;	, clin_cat_cv.collation_seq
	, p_comp.sequence
	, pw_os.order_sentence_seq

with	time = 60
